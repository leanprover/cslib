/-
Copyright (c) 2025 Tanner Duve. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tanner Duve, Bhavik Mehta
-/
import Mathlib.Algebra.Group.Defs
import Mathlib.Algebra.Group.Pointwise.Set.Basic
import Mathlib.Algebra.Ring.Idempotent
import Mathlib.Data.Set.Basic
import Mathlib.Order.Closure
import Mathlib.Tactic.Cases
import Cslib.Logics.LinearLogic.CLL.Basic

/-!
# Phase semantics for Classical Linear Logic

This file develops the phase semantics for Classical Linear Logic (CLL), providing an
algebraic interpretation of linear logic propositions in terms of phase spaces.

Phase semantics is a denotational semantics for linear logic where
propositions are interpreted as subsets of a commutative monoid, and logical operations
correspond to specific set-theoretic operations.

## Main definitions

* `PhaseSpace`: A commutative monoid equipped with a distinguished subset âŠ¥
* `PhaseSpace.imp`: Linear implication `X âŠ¸ Y` between sets in a phase space
* `PhaseSpace.orthogonal`: The orthogonal `Xâ« ` of a set X
* `PhaseSpace.isFact`: A fact is a set that equals its biorthogonal closure
* `Fact`: The type of facts in a phase space
* `PhaseSpace.FactExpr`: Inductive type for representing operations on facts
* `PhaseSpace.interpret`: Interpretation of the connectives on facts
* `PhaseSpace.interpProp`: Interpretation of CLL propositions as facts in a phase space

## Main results

* `PhaseSpace.biorthogonalClosure`: The biorthogonal operation forms a closure operator
* `PhaseSpace.orth_iUnion`: Orthogonal of union equals intersection of orthogonals
* `PhaseSpace.sInf_isFact` and `PhaseSpace.inter_isFact_of_isFact`: Facts are closed under
  intersections
* `PhaseSpace.biorth_least_fact`: The biorthogonal closure gives the smallest fact containing a set

Several lemmas about facts and orthogonality useful in the proof of soundness are proven here.

## TODO
- Soundness theorem
- Completeness theorem

## References

* [J.-Y. Girard, *Linear Logic: its syntax and semantics*][Girard1995]
-/

universe u v

namespace CLL

open Proposition
open scoped Pointwise
open Set

/--
A phase space is a commutative monoid M equipped with a distinguished subset âŠ¥.
This forms the algebraic foundation for interpreting linear logic propositions.
-/
class PhaseSpace (M : Type u) extends CommMonoid M where
  /-- The distinguished subset âŠ¥ used to define orthogonality -/
  bot : Set M

namespace PhaseSpace

variable {P : Type*} [PhaseSpace P] {p q : P}

section

-- ## Basic operations

/--
Linear implication `X âŠ¸ Y` in a phase space: the set of elements m such that
for all x âˆˆ X, we have m * x âˆˆ Y.
-/
def imp [PhaseSpace M] (X Y : Set M) : Set M := {m | âˆ€ x âˆˆ X, m * x âˆˆ Y}

@[inherit_doc] scoped infix:50 " âŠ¸ " => imp

/--
The orthogonal `Xâ« ` of a set X: the set of elements that map X into âŠ¥ under multiplication.
-/
def orthogonal [PhaseSpace M] (X : Set M) : Set M := X âŠ¸ bot

@[inherit_doc] scoped postfix:max "â« " => orthogonal

-- ## Properties of orthogonality

@[simp] lemma orthogonal_def [PhaseSpace M] (X : Set M) : Xâ«  = {m | âˆ€ x âˆˆ X, m * x âˆˆ bot} := rfl

/--
The orthogonal operation is antitone: if X âŠ† Y then Yâ«  âŠ† Xâ« .
-/
lemma orth_antitone [PhaseSpace M] {X Y : Set M} (hXY : X âŠ† Y) :
    Yâ«  âŠ† Xâ«  := by
  intro m hm x hx
  exact hm x (hXY hx)

/--
The biorthogonal operation is extensive: X âŠ† Xâ« â«  for any set X.
-/
lemma orthogonal_extensive [PhaseSpace M] (X : Set M) : X âŠ† Xâ« â«  := by
  intro x hx n hn
  simpa [orthogonal, imp, Set.mem_setOf, mul_comm] using hn x hx

/--
The triple orthogonal equals the orthogonal: Xâ« â« â«  = Xâ« .
-/
lemma triple_orth [PhaseSpace M] (X : Set M) : Xâ« â« â«  = Xâ«  := by
  apply le_antisymm
  Â· intro m hm x hxX
    have hx' : x âˆˆ (Xâ« )â«  := by
      intro y hy
      simpa [orthogonal, imp, Set.mem_setOf, mul_comm] using hy x hxX
    exact hm x hx'
  Â· apply orthogonal_extensive (X := Xâ« )

lemma triple_dual {G : Set P} : Gâ« â« â« â«  = Gâ« â«  := triple_orth Gâ« 
/--
The biorthogonal closure operator on sets in a phase space.
-/
def biorthogonalClosure [PhaseSpace M] : ClosureOperator (Set M) := {
  toFun X := Xâ« â« 
  monotone' := by
    intro X Y hXY m hm n hnY
    have hnX : n âˆˆ Xâ«  := by
      intro x hxX
      exact hnY x (hXY hxX)
    exact hm n hnX
  le_closure' := by
    intro X x hx n hn
    simpa [orthogonal, imp, Set.mem_setOf, mul_comm] using hn x hx
  idempotent' X := triple_orth Xâ« 
}

-- # Basic theory of phase spaces

/--
Given a phase space (P, âŠ¥) and a set of subsets (Gáµ¢)_{i âˆˆ I} of P, we have that
(â‹ƒáµ¢ Gáµ¢)â«  = â‹‚áµ¢ Gáµ¢â« .
-/
lemma orth_iUnion [PhaseSpace M] {Î¹ : Sort*} (G : Î¹ â†’ Set M) :
    (â‹ƒ i, G i)â«  = â‹‚ i, (G i)â«  := by
  ext m; constructor
  Â· intro hm
    have hm' : âˆ€ x âˆˆ â‹ƒ j, G j, m * x âˆˆ PhaseSpace.bot := by
      simpa [orthogonal, imp, mem_setOf] using hm
    refine mem_iInter.mpr (fun i => ?_)
    exact fun x hx => hm' x (mem_iUnion.mpr âŸ¨i, hxâŸ©)
  Â· intro hm x hx
    rcases mem_iUnion.mp hx with âŸ¨i, hixâŸ©
    have hmi : m âˆˆ (G i)â«  := mem_iInter.mp hm i
    simpa [orthogonal, imp, mem_setOf] using hmi x hix

/--
Given a phase space (P, âŠ¥) and a set of subsets (Gáµ¢)_{i âˆˆ I} of P, we have that
âˆ©áµ¢ Gáµ¢â« â«  = (âˆªáµ¢ Gáµ¢â« )â« .
-/
lemma iInter_biorth_eq_orth_iUnion_orth [PhaseSpace M] {Î¹ : Sort*} (G : Î¹ â†’ Set M) :
    (â‹‚ i, (G i)â« â«  : Set M) = (â‹ƒ i, (G i)â« )â«  := by
  simpa using (orth_iUnion (M := M) (G := fun i => (G i)â« )).symm

-- ## Facts

/--
A fact is a subset of a phase space that equals its biorthogonal closure.
-/
def isFact [PhaseSpace M] (X : Set M) : Prop := X = Xâ« â« 

/--
The type of facts in a phase space.
-/
structure Fact (P : Type*) [PhaseSpace P] where
  (carrier : Set P)
  (property : isFact carrier)

instance : SetLike (Fact P) P where
  coe := Fact.carrier
  coe_injective' := fun p q h => by cases p; cases q; congr

instance [PhaseSpace M] : Coe (Fact M) (Set M) := âŸ¨Fact.carrierâŸ©

initialize_simps_projections Fact (carrier â†’ coe)

@[simp] lemma mem_carrier (G : Fact P) : G.carrier = (G : Set P) := rfl

lemma subset_dual_dual (G : Set P) : G âŠ† Gâ« â«  := fun p hp q hq => mul_comm p q â–¸ hq _ hp

@[simps] def Fact.mk_subset (G : Set P) (h : Gâ« â«  âŠ† G) : Fact P where
  carrier := G
  property := by simp only [isFact]; symm; apply h.antisymm (subset_dual_dual G)

lemma dual_subset_dual {G H : Set P} (h : G âŠ† H) : Hâ«  âŠ† Gâ«  := fun _ hp _ hq => hp _ (h hq)

@[simps!] def Fact.mk_dual (G H : Set P) (h : G = Hâ« ) : Fact P :=
  Fact.mk_subset G <| by rw [h, triple_orth]

lemma coe_mk {X : Set P} {h : isFact X} :
    ((âŸ¨X, hâŸ© : Fact P) : Set P) = X := rfl

@[simp] lemma closed (F : Fact P) : isFact (F : Set P) := F.property

@[simp] lemma top_isFact :
    isFact (univ : Set P) := by
  rw [isFact]; symm
  simpa only [top_eq_univ]
    using ClosureOperator.closure_top (CLL.PhaseSpace.biorthogonalClosure (M:=P))

/-- In any phase space, `{1}â«  = âŠ¥`. -/
lemma orth_one_eq_bot :
    ({(1 : P)} : Set P)â«  = (PhaseSpace.bot : Set P) := by
  ext m; constructor
  Â· intro hm
    simpa [orthogonal, imp, mem_setOf, mul_one] using hm 1 (by simp)
  Â· intro hm x hx
    rcases hx with rfl
    simpa [orthogonal, imp, mem_setOf, mul_one] using hm

/-- `0 := âŠ¤â« ` is a fact and is the smallest fact. -/
@[simp] lemma zero_isFact : isFact ((âˆ… : Set P)â« â« ) := by
  simp only [isFact, triple_orth]

/--
A set is a fact if and only if it is the orthogonal of some set
-/
lemma fact_iff_exists_orth (X : Set P) :
    isFact X â†” âˆƒ Y : Set P, X = Yâ«  := by
  constructor
  Â· intro hX
    refine âŸ¨Xâ« , ?_âŸ©
    exact hX
  Â· rintro âŸ¨Y, rflâŸ©
    simp only [isFact, triple_orth (X := Y)]

/-- The fact given by the dual of G. -/
@[simps!] def dualFact (G : Set P) : Fact P := Fact.mk_dual (Gâ« ) G rfl

/-- `âŠ¥` is a fact. -/
@[simp] lemma bot_isFact : isFact (PhaseSpace.bot : Set P) := by
  refine (fact_iff_exists_orth (X := (PhaseSpace.bot : Set P))).2 ?_
  exact âŸ¨{(1 : P)}, (orth_one_eq_bot).symmâŸ©

/--
The interpretation of the multiplicative unit 1: the biorthogonal closure of {1}.
-/
def oneSet [PhaseSpace P] : Set P := ({1} : Set P)â« â« 

@[simp] lemma oneSet_isFact : isFact (oneSet : Set P) := by
  simp only [oneSet, isFact, triple_orth]

/--
The dual of oneSet is a fact.
-/
lemma oneSet_dual_isFact : isFact (oneSetâ«  : Set P) := by
  simp only [oneSet, isFact, triple_orth]

/--
If Y is a fact, then X âŠ¸ Y is also a fact
-/
lemma imp_isFact_of_fact (X Y : Set P) (hY : isFact Y) :
    isFact (X âŠ¸ Y) := by
  have hXY : (X âŠ¸ Y) = (X * Yâ« )â«  := by
    ext m
    constructor
    Â· intro hm z hz
      rcases hz with âŸ¨x, hxX, y, hyYperp, rflâŸ©
      have hmx : m * x âˆˆ Y := hm x hxX
      have : y * (m * x) âˆˆ bot := hyYperp (m * x) (by simpa using hmx)
      simpa [mul_left_comm, mul_comm, mul_assoc] using this
    Â· intro hm x hxX
      have hxYbi : m * x âˆˆ Yâ« â«  := by
        intro y hy
        have : m * (x * y) âˆˆ bot := hm (x * y) âŸ¨x, hxX, y, hy, rflâŸ©
        simpa [mul_assoc, mul_left_comm, mul_comm] using this
      rw [hY]; exact hxYbi
  simp only [isFact, hXY, triple_orth]

/-- In a phase space, `Gâ« â« ` is the smallest fact containing `G`. -/
lemma biorth_least_fact (G : Set P) :
      âˆ€ {F : Set P}, isFact F â†’ G âŠ† F â†’ Gâ« â«  âŠ† F := by
  let c : ClosureOperator (Set P) := biorthogonalClosure
  have h_min :
      âˆ€ {F : Set P}, isFact F â†’ G âŠ† F â†’ Gâ« â«  âŠ† F := by
    intro F hF hGF
    have hF_closed : c.IsClosed F := by
      have : F = c F := by simpa [isFact, c] using hF
      exact (c.isClosed_iff).2 this.symm
    simpa [c] using ClosureOperator.closure_min hGF hF_closed
  apply h_min

/-- `0` is the least fact (w.r.t. inclusion). -/
lemma zero_least_fact :
    âˆ€ {F : Set P}, isFact F â†’ ((âˆ… : Set P)â« â« ) âŠ† F := by
  intro F hF
  have h := biorth_least_fact (G := (âˆ… : Set P)) (F := F) hF
              (by simp)
  simpa using h

/-- `âŠ¤ = âˆ…â« `, so `âŠ¤` is a fact. -/
@[simp] lemma top_eq_orth_empty :
  (Set.univ : Set P) = (âˆ… : Set P)â«  := by
  ext m; simp [orthogonal, imp]

/--
Linear implication between a set and a fact, yielding a fact.
-/
def Fact.imp (X : Set P) (Y : Fact P) : Fact P :=
  âŸ¨ X âŠ¸ Y, imp_isFact_of_fact X Y Y.property âŸ©

lemma isFact_iff_closed (X : Set P) :
  isFact X â†” biorthogonalClosure.IsClosed X := by
  constructor <;> (intro; simp only [isFact, biorthogonalClosure]; symm; assumption)

/--
Arbitrary intersections of facts are facts.
-/
lemma sInf_isFact {S : Set (Set P)}
    (H : âˆ€ X âˆˆ S, isFact X) : isFact (sInf S) := by
  have H' : âˆ€ X âˆˆ S, biorthogonalClosure.IsClosed X :=
    fun X hX => (isFact_iff_closed (X := X)).1 (H X hX)
  have : biorthogonalClosure.IsClosed (sInf S) :=
    ClosureOperator.sInf_isClosed (c := biorthogonalClosure) (S := S) H'
  exact (isFact_iff_closed (X := sInf S)).2 this

/--
Binary intersections of facts are facts.
-/
lemma inter_isFact_of_isFact {A B : Set P}
    (hA : isFact A) (hB : isFact B) : isFact (A âˆ© B) := by
  have : isFact (sInf ({A,B} : Set (Set P))) := sInf_isFact (by
    intro X hX; rcases hX with rfl | rfl | _; simp [hA]; simp [hB])
  simpa [sInf_insert, sInf_singleton, inf_eq_inter] using this

-- instance : InfSet (Fact P) where
--   sInf S := @sInf_isFact P _ S

-- @[simp] lemma coe_sInf {S : Set (Fact P)} : ((sInf S : Fact P) : Set P) = â‹‚ i âˆˆ S, i := rfl

-- instance : Min (Fact P) where
--   min G H := Fact.mk_dual (G âˆ© H) (dual G âˆª dual H) <| by simp


/-- `ðŸ­ := {1}â« â«  = âŠ¥â« ` -/
lemma oneSet_eq_bot_orth :
    (oneSet : Set P) = (PhaseSpace.bot : Set P)â«  := by
  simp only [oneSet, orth_one_eq_bot]

/-- for any fact `G`, we have `ðŸ­ Â· G = G` -/
lemma one_mul_fact_set (G : Fact P) :
    (oneSet : Set P) * (G : Set P) = (G : Set P) := by
  apply le_antisymm
  Â· intro z hz
    rcases hz with âŸ¨a, ha, q, hq, rflâŸ©
    have : a * q âˆˆ ((G : Set P)â« â« ) := by
      intro y hy
      have hyq : y * q âˆˆ (PhaseSpace.bot : Set P) := by
        simpa [orthogonal, imp, Set.mem_setOf] using hy q hq
      have ha' : a âˆˆ (PhaseSpace.bot : Set P)â«  := by
        simpa [oneSet_eq_bot_orth] using ha
      have : a * (y * q) âˆˆ PhaseSpace.bot := ha' _ hyq
      simpa [mul_left_comm, mul_comm, mul_assoc] using this
    have h : isFact (G : Set P) := G.property
    rw [h]; exact this
  Â· intro g hg
    have h1 : (1 : P) âˆˆ (oneSet : Set P) := by
      have : (1 : P) âˆˆ ({(1 : P)} : Set P) := by simp
      exact orthogonal_extensive _ this
    exact âŸ¨1, h1, g, hg, by simpâŸ©

/--
The idempotent elements within a given set X.
-/
def idempotentsIn [Monoid M] (X : Set M) : Set M := {m | IsIdempotentElem m âˆ§ m âˆˆ X}

/--
The set I of idempotents that "belong to 1" in the phase semantics.
-/
def I [PhaseSpace M] : Set M := idempotentsIn (oneSet : Set M)

-- ## Interpretation of the connectives

inductive const where
| one | zero | top | bot

inductive unop where
| bang | quest

inductive binop where
| tensor | parr | with | oplus

inductive FactExpr where
| const (c : const) : FactExpr
| unop (u : unop) (X : Fact P) : FactExpr
| binop (b : binop) (X Y : Fact P) : FactExpr

def constInterpret (c : const) : Fact P :=
match c with
| .one => âŸ¨oneSet, oneSet_isFactâŸ©
| .zero => âŸ¨(âˆ… : Set P)â« â« , zero_isFactâŸ©
| .top => âŸ¨(Set.univ : Set P), top_isFactâŸ©
| .bot => âŸ¨oneSetâ« , oneSet_dual_isFactâŸ©

def unopInterpret (u : unop) (X : Fact P) : Fact P := match u with
| .bang => dualFact (X âˆ© I)â« 
| .quest => dualFact (Xâ«  âˆ© I)

def binopInterpret (b : binop) (X Y : Fact P) : Fact P := match b with
| .tensor => dualFact (X * Y)â« 
| .parr => dualFact ((Xâ« ) * (Yâ« ))
| .with => sorry
| .oplus => dualFact (X âˆª Y)â« 

def interpret (c : @FactExpr P _) : Fact P := match c with
| .const c => constInterpret c
| .unop u X => unopInterpret u X
| .binop b X Y => binopInterpret b X Y

-- ## Interpretation of propositions

/--
The interpretation of a CLL proposition in a phase space, given a valuation of atoms to facts.
-/
def interpProp [PhaseSpace M] (v : Atom â†’ Fact M) : Proposition Atom â†’ Fact M
  | .atom a       => v a
  | .atomDual a   => âŸ¨(v a)â« , by rw [fact_iff_exists_orth]; use (v a)âŸ©
  | .one          => interpret (.const .one)
  | .zero         => interpret (.const .zero)
  | .top          => interpret (.const .top)
  | .bot          => interpret (.const .bot)
  | .tensor A B   => interpret (.binop .tensor (interpProp v A) (interpProp v B))
  | .parr   A B   => interpret (.binop .parr   (interpProp v A) (interpProp v B))
  | .with   A B   => interpret (.binop .with   (interpProp v A) (interpProp v B))
  | .oplus  A B   => interpret (.binop .oplus  (interpProp v A) (interpProp v B))
  | .bang   A     => interpret (.unop  .bang   (interpProp v A))
  | .quest  A     => interpret (.unop  .quest  (interpProp v A))

@[inherit_doc] scoped notation:max "âŸ¦" P "âŸ§" v:90 => interpProp v P
